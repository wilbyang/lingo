import org.flywaydb.gradle.task.*

buildscript {
    dependencies {
        classpath "com.mysql:mysql-connector-j:$mysqlVersion"
        classpath "org.flywaydb:flyway-mysql:$flywayVersion"
    }
}

plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id "org.flywaydb.flyway" version "${flywayVersion}"
    id 'com.libwy.lingo'
}

style {
    excludePackages = [
    ]
    excludeClasses = [
    ]
}

coverage {
    excludePackages = [
    ]
    excludeClasses = [
    ]
}

dependencies {
    implementation(project(":libwy-lingo-core"))
    implementation("org.springframework.boot:spring-boot-starter:$springBootVersion")
    implementation("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
    runtimeOnly("com.mysql:mysql-connector-j:$mysqlVersion")
    testImplementation("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
    testImplementation("io.cucumber:cucumber-java8:$cucumberVersion")
    testImplementation("io.cucumber:cucumber-junit:$cucumberVersion")
    testImplementation("io.cucumber:cucumber-junit-platform-engine:$cucumberVersion")
    testImplementation("io.cucumber:cucumber-spring:$cucumberVersion")
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation, runtimeOnly
    }
}

tasks.register('cucumber') {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'com.libwy.lingo.api.stepdef', 'src/test/resources']
        }
    }
}

tasks.register('migrateToDev', FlywayMigrateTask) {
    url = 'jdbc:mysql://localhost:3306/todo_dev?useUnicode=true&characterEncoding=utf-8&useSSL=false&allowPublicKeyRetrieval=true'
    user = 'root'
    password = 'yangbo'
    locations = ["filesystem:$rootDir/gradle/config/migration"]
}

tasks.register('migrateToTest', FlywayMigrateTask) {
    url = 'jdbc:mysql://localhost:3306/todo_test?useUnicode=true&characterEncoding=utf-8&useSSL=false&allowPublicKeyRetrieval=true'
    user = 'root'
    password = 'yangbo'
    locations = ["filesystem:$rootDir/gradle/config/migration"]
}

tasks.register('migrate') {
}

migrate.dependsOn migrateToDev, migrateToTest

springBoot {
    mainClass = 'com.libwy.lingo.api.Bootstrap'
}

bootJar {
    archiveClassifier = 'boot'

    // https://github.com/spring-projects/spring-boot/wiki/Spring-Boot-1.4-Release-Notes#jersey-classpath-scanning-limitations
    requiresUnpack '**/todo-*.jar'

    launchScript()
}